#pragma once

#include "/Engine/Private/Common.ush"
#include "UVs/CustomUVs.ush"

// sand dissolve simulation on uv planner
float3 SandDissolvePlanner(Texture2D NoiseTexture, SamplerState NoiseSampler,
	float2 resolution,
	float2 UVs,
	float time,
	float2 speedMove,
	float speedSparking,
	float alpha)
{
	float2 computedSampleUV = (UVs + floor(time * speedMove * resolution)) / resolution;

	float3 noiseColor = Texture2DSample(NoiseTexture, NoiseSampler, computedSampleUV);
	float3 noiseSparking = saturate(frac(noiseColor + time * speedSparking));
	
	float3 noiseWithAlpha = step(noiseSparking, alpha) * saturate(noiseSparking / alpha);
	
	return  noiseWithAlpha;
}

// sand dissolve simulation on z direction
float3 SandDissolveZDepth(
	Texture2D NoiseTexture,
	SamplerState NoiseSampler,
	float2 Resolution,
	float2 CoordInput,
	float time,
	float2 speedMove,
	float speedSparking,
	float alpha,
	float2 centerPos,
	float2 radialScale)
{
	float2 computedUVsInput = RadialUVs(CoordInput,centerPos) * radialScale;
	float2 computedSampleUV_Main = (computedUVsInput + floor(time * speedMove * Resolution)) / Resolution;
	float3 noiseColor_main = Texture2DSample(NoiseTexture, NoiseSampler, computedSampleUV_Main);

	float2 computedSampleUV_Mask = CoordInput / Resolution;
	float3 noiseColor_mask = Texture2DSample(NoiseTexture, NoiseSampler, computedSampleUV_Mask);
	float3 noiseColor_mask_sparking = saturate(frac(noiseColor_mask + time * speedSparking));

	float3 noiseSparkingResult = lerp(noiseColor_mask_sparking * noiseColor_main, noiseColor_main, 0.5f);
	
	float3 noiseWithAlpha = step(noiseSparkingResult, alpha) * saturate(noiseSparkingResult / alpha);
	
	return noiseWithAlpha;
}